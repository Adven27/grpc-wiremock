buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.12'
    }
}

plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
}

apply plugin: 'com.google.protobuf'

group = 'io.adven.grpc.wiremock'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

repositories {
    mavenCentral()
}

def grpcVersion = '1.29.0'
def protobufVersion = '3.12.1'
def protocVersion = protobufVersion

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation "io.grpc:grpc-all:${grpcVersion}"
    implementation 'com.github.tomakehurst:wiremock-standalone:2.26.3'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" } }
    generateProtoTasks { all()*.plugins { grpc { outputSubDir = 'java' } } }
    generatedFilesBaseDir = 'src'
}

sourceSets.main.proto.srcDir '/proto'

task generateJava(type: Copy) {
    def decorate_services = ""
    def import_services = ""
    def services = ""
    doFirst {
        def callDecoratorTemplate = """
        @Around("execution(* @package@.@service@Grpc.@service@ImplBase.*(..))")
        public void redirect@service@(ProceedingJoinPoint jp) throws Throwable {
            Object[] args = jp.getArgs();
            String method = jp.getStaticPart().getSignature().getName();
            ServerCallStreamObserver observer = (ServerCallStreamObserver) args[1];
            observer.onNext(httpMock.send(args[0], "@service@/" + method, respType(method, "@package@.@service@Grpc\$@service@BlockingStub")));
            observer.onCompleted();
        }"""
        def importServiceTemplate = "import @package@.@service@Grpc;\n"
        def serviceTemplate = "@Service class @service@ extends @service@Grpc.@service@ImplBase {}\n"
        new FileNameFinder().getFileNames("$projectDir/src/main/java", '**/*Grpc.java').forEach {
            def path = new File(it).path
            def relativePath = path.split("src/main/java/")[1]
            def pkg = relativePath.substring(0, relativePath.lastIndexOf("/")).replace("/", ".")
            def srv = relativePath.substring(relativePath.lastIndexOf("/") + 1).replace("Grpc.java", "")
            println "Found $path: "
            println "Parsed package = [$pkg] and service = [$srv]"

            decorate_services += callDecoratorTemplate.replace("@package@", pkg).replace("@service@", srv)
            import_services += importServiceTemplate.replace("@package@", pkg).replace("@service@", srv)
            services += serviceTemplate.replace("@package@", pkg).replace("@service@", srv)
        }
    }
    from "src/template/java"
    into "src/main/java/io/adven/grpc/wiremock"
    filter { it.replace("@import_services@", import_services).replace("@services@", services).replace("@decorate_services@", decorate_services) }
}

compileJava.dependsOn 'generateJava'
generateJava.dependsOn 'generateProto'